ratings = LOAD '/user/maria_dev/ml-100k/u.data' AS (userID:int, movieID:int, rating:int, ratingTime:int);

metadata = LOAD '/user/maria_dev/ml-100k/u.item' USING PigStorage('|') AS (movieID:int, movieTitle:chararray, releaseDate:chararray, videoRelease:chararray, imdbLink:chararray);

nameLookup = FOREACH metadata GENERATE movieID, movieTitle, ToUnixTime(ToDate(releaseDate, 'dd-MMM-yyyy')) as releaseTime;

ratingByMovie = GROUP ratings by movieID;

avgRatings = FOREACH ratingByMovie GENERATE group AS movieID, COUNT(ratings.rating) as numCount, AVG(ratings.rating) as avgRating;

oneStar = FILTER avgRatings BY avgRating < 2.0;

oneStarWithName= JOIN oneStar BY movieID, nameLookup BY movieID;

oldestOneStar = ORDER oneStarWithName BY nameLookup::releaseTime;

DUMP oldestOneStar;